plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = 'wtf.mlsac'
version = '1.0'


repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc-releases"
        url = "https://repo.codemc.io/repository/maven-releases/"
    }
    maven {
        name = "codemc-snapshots"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'
    implementation 'com.github.retrooper:packetevents-spigot:2.7.0'
    implementation 'com.google.flatbuffers:flatbuffers-java:23.5.26'
    implementation 'com.microsoft.signalr:signalr:8.0.0'
    implementation 'com.microsoft.signalr.messagepack:signalr-messagepack:8.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:32.1.3-jre'
    
    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'net.jqwik:jqwik:1.8.3'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:1.5.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    
    relocate 'com.google.flatbuffers', 'net.mlsac.libs.flatbuffers'
    relocate 'com.github.retrooper.packetevents', 'net.mlsac.libs.packetevents.api'
    relocate 'io.github.retrooper.packetevents', 'net.mlsac.libs.packetevents.impl'
    relocate 'com.google.common', 'net.mlsac.libs.guava'
    relocate 'com.google.gson', 'net.mlsac.libs.gson'
    relocate 'com.microsoft.signalr', 'net.mlsac.libs.signalr'
    relocate 'org.msgpack', 'net.mlsac.libs.msgpack'
    relocate 'okhttp3', 'net.mlsac.libs.okhttp3'
    relocate 'okio', 'net.mlsac.libs.okio'
}

build.dependsOn shadowJar

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'jqwik'
    }
}

